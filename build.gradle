plugins {
    id 'java'
    id 'maven-publish'
}

allprojects {
    group = "be.theking90000.mclib2"
    version = "0.0.2-SNAPSHOT"

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "java-library"

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }
        withSourcesJar()
        withJavadocJar()
    }

    tasks.withType(Jar).configureEach {
        // Use the same logic as for artifactId
        archiveBaseName.set(
                project.path.replace(":", "-").replaceFirst("-", "")
        )
    }

    if (file("$projectDir/build.gradle").exists()) {
        publishing {
            publications {
                mavenJava(MavenPublication) {
                    from components.java
                    groupId = project.group
                    artifactId = project.path.replace(":", "-").replaceFirst("-", "") // e.g. core-runtime
                    version = project.version
                }
            }

            repositories {
                maven {
                    name "theking90000Repo"
                    url = uri(version.endsWith("SNAPSHOT") ?
                                    "https://repo.theking90000.be/snapshots" :
                                    "https://repo.theking90000.be/releases")
                    credentials {
                        username = "$theking90000RepoUser"
                        password = "$theking90000RepoPassword"
                    }
                }
            }
        }

    }
}